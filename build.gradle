buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url "https://plugins.gradle.org/m2/" }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}"
def env = System.getenv()
if (env.BUILD_NUMBER) {
	version = version + "+" + "${env.BUILD_NUMBER}"
}
group = 'de.melanx.vanillaexcavators'
archivesBaseName = "vanillaexcavators-${mc_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: 'snapshot', version: mappings_version

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'

			mods {
				vanillaexcavators {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run_server')

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'

			mods {
				vanillaexcavators {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run_data')

			// Recommended logging data for a userdev environment
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			// Recommended logging level for the console
			property 'forge.logging.console.level', 'debug'

			args '--mod', 'vanillaexcavators', '--all', '--output', '"' + file('src/generated/resources/') + '"', '--existing', '"' + file('src/main/resources/') + '"'

			mods {
				vanillaexcavators {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	maven {
		// location of the maven that hosts JEI files
		name = "Progwml6 maven"
		url = "https://dvs1.progwml6.com/files/maven/"
	}
	maven {
		url = "https://maven.melanx.de"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
	runtimeOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.2")
	compile fg.deobf("de.melanx.morevanillalib:morevanillalib-1.15.2:1.2+5")
}

jar {
	manifest {
		attributes([
				"Specification-Title": "vanillaexcavators",
				"Specification-Vendor": "vanillaexcavators",
				"Specification-Version": "1", // We are version 1 of ourselves
				"Implementation-Title": project.name,
				"Implementation-Version": "${version}",
				"Implementation-Vendor" :"vanillaexcavators",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier 'deobf'
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			artifact jar
			artifact sourcesJar
			artifact deobfJar
		}
	}
}

publishing {
	repositories {
		maven {
			url "/var/www/maven"
		}
	}
}